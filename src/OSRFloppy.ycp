/**
 *  File:
 *    OSRFloppy.ycp
 *
 *  Module:
 *     Read/Write files from floppy with dialog support.
 * 
 *  Summary:
 *      How to use OSRFloppy module:
 *	1. OSRFloppy::Open( with ui support, count of files to read/wirte );
 *
 *      2. optional: OSRFloppy::NextStep(_(Copy the file ..  ));
 *         WFM::Execute(.local.bash, "/bin/cp ... "); or OSRFloppy::ReadMap( .. ) or OSRFloppy::SaveMap( .. )
 *
 * 	3. OSRFloppy::Close();
 *
 *  Author:
 *    Johannes Buchhold <jbuch@suse.de>
 *
 * $Id$
 */
{
  module "OSRFloppy";
   
  textdomain "repair";

  import "StorageDevices";
  import "Report";
  import "OSRModuleLoading";
  import "Partitions";


  /**
   * Default mount point
   */
  global string mount_point            = "/media/floppy";

  /**
   * Default floppy device
   */
  global string floppy_device	       = StorageDevices::FloppyDevice;

 
  /**
   * Supported file systems
   */
  list(string) needed_fs_kernel_modules        = ["fat", "vfat" ];
  
  /**
   * Only for internal use.
   */
  boolean dialog_open	        = false;
  boolean use_ui		= true;
  integer step			= 0;
  global boolean opened 	= false;


  
  /**
   * Reset the internal values. 
   */
  define void Reset(boolean local_use_ui)``{
      use_ui = local_use_ui;
      step  = 0;
      mount_point = "/media/floppy";
  }
  

  /**
   * Check if a floppy device exists.
   */
  define boolean CheckPresent()``{

      if ( ! StorageDevices::FloppyPresent )
      {
	  //TODO check english
	  string message = _("
No floppy device was detected. Loading a
file from a removable device is not possible.
");

	  if ( use_ui ) {
	      Report::Error(message);
	  }
	  else {
	      y2error(message);
	  }
	  return false;
      }
      return true;
  }
 

  /**
   * Load needed fs modules.
   */
  define boolean LoadFsModules()``{
      boolean error = false;
      
      foreach( `module_name, needed_fs_kernel_modules, ``{

	  if ( ! error ) {

	      if ( ! OSRModuleLoading::Load( module_name,"","","",false, true ) )	{
		  //TODO check english
		  string message = sformat(_("
An error occurred while loading the %1 kernel
module. Mounting the floppy device is not possible.
System information cannot be read.

"), "");
		  if ( use_ui )
		  {
		      Report::Error(message);
		  }
		  else {
		      y2error(message );
		  }
		  error = true;
	      }
	  }
      });
      return ! error;
  }

  /**
   * Check the mount point! If the mount point does not exist
   * CheckMountPoint create it.
   */
  define boolean CheckMountPoint()``{

      string command  =  sformat("/usr/bin/test -d %1", mount_point );

      if (  WFM::Execute(.local.bash, command) != 0 )  {

	  //TODO check english
	  string message = _("
The default floppy mount point \"/media/floppy\"
does not exist and cannot be created.
");
	  
	  if ( WFM::Execute(.local.bash, sformat("if /usr/bin/test -d %1; then /bin/mkdir %1; fi", "/media" ) ))
	  {
	      if ( WFM::Execute(.local.bash, sformat("if /usr/bin/test -d %1; then /bin/mkdir %1; fi", "/media/floppy" ) ))
	      {
		  return true;
	      }	
	  }
	  
	  if ( use_ui ) {
	      Report::Error(message);
	  }
	  else {
	      y2error(message);
	  }
	  return false;
      }
      return true;	
  }


  /**
   * Mount default floppy device
   */
  define boolean Mount()``{

      //WFM::Execute(.local.bash, "/bin/umount " + mount_point );
      string mpoint = Partitions::MountedOn( floppy_device, Partitions::CurMounted() );
      if ( mpoint != "") {
	  y2milestone("Floppy is already mounted on %1", mpoint );
	  mount_point = mpoint;
	  return true;
      }


      if ( WFM::Execute(.local.bash, "/bin/mount " + floppy_device + " "+mount_point+ " -t auto" ) != 0 )
      {
	    //TODO check english
	  string message = _("\nAn error occurred while mounting the floppy.
");

	  if ( use_ui )
	  {
	      Report::Error(message);
	  }
	  else {
	      y2error(message );
	  }
	  
	  return  false;
      }
      return true;
  }


  
  /**
   * Unmount floppy device
   */
  define boolean Umount()``{
      
      if( Partitions::MountedOn( floppy_device, Partitions::CurMounted() ) == "" )
	  return true;
      
      if (  WFM::Execute(.local.bash, "/bin/umount " +mount_point ) != 0 )
      {
	    //TODO check english
	  string message = _("An error occurred while unmounting the floppy.");

	  if ( use_ui )
	  {
	      Report::Error(message);
	  }
	  else {
	      y2error(message );
	  }
	  
	  return  false;
      }
      return true;
  }

  

  /**
   * Open the floppy dialog
   */
  define boolean OpenFloppyDialog(string label, integer steps)``{
      
      if ( UI::OpenDialog(
		 `VBox(
		       `ProgressBar(`id(`progress), label, steps, 0),
		       `VSpacing(0.4),
		       `PushButton(`id(`cancel ) , UI::CancelButtonLabel() )
                     )
		 ) )
      {
	  UI::SetFocus(`id(`cancel ));
	  dialog_open = true;
	  return true;
      }
      
      y2error("Floppy Dialog could not be opened.");
      return false;
  }

  /**
   * Change the text in the floppy dialog and increase the progress bar.
   * If not dialog is opened, write the test to the YaST2 log file (y2log).
   */
  global define void NextStep( string label ) ``{
      y2milestone( label );
      if ( dialog_open && use_ui ) {
	  step = step + 1;
	  UI::ChangeWidget(`id(`progress), `Value, step);
	  UI::ChangeWidget(`id(`progress), `Label, label );
      }
      else {
	  y2milestone( label );
      }
  }

  /**
   * Close the floppy dialog if the dialog is opened.
   */
 define boolean CloseFloppyDialog()``{
      if ( dialog_open ) {
	  if ( UI::CloseDialog() ) {
	      dialog_open = false;
	      return true;
	  }
	  y2error("Floppy Dialog could not be closed");
	  return false;
      }
      return true;
  }

 /**
  * Open floppy for IO.
  * Afterwards you must call OSRFloppy::Close().
  */
  global define boolean Open(boolean use_ui, integer count_io_files )``{

      if( opened ) {
	  y2error("Floppy is already opened. Please use
CloseFloppy(...) to close floppy device.");
	  return false;
      }
      opened = true;

      Reset( use_ui );

      if  ( use_ui ) OpenFloppyDialog( _("Checking floppy device..."), count_io_files + 4 );
      if ( ! CheckPresent()) return false;
     
      NextStep(_("Loading file system modules...") );
      if ( ! LoadFsModules()) return false;

      NextStep(_("Checking mount point..."));
      if ( ! CheckMountPoint()) return false;

      NextStep(_("Mounting floppy...")); 
      if ( ! Mount()) return false;

    
      return true;
  }

  /**
   * Close Floppy Dialog and unmount floppy device.
   * You must call OSRFloppy::Open(.. ) before.
   */
  global define boolean Close()``{

      if( ! opened ) {
	  y2error("Floppy is not opend. Could not close floppy device.");
	  return false;
      }
      
      boolean error = false;
      NextStep(_("Unmounting floppy...")); 
      if ( ! Umount())  error = true;

      if ( use_ui ) CloseFloppyDialog();

      opened = false;
      return ! error;
  }
  

  
  /**
   * Read file from mounted floppy device.
   * You must call OSRFloppy::Open( ) before and afterwards OSRFloppy::Close().
   */
  global define map ReadMap(string filename, string text )``{

      if( ! opened ) {
	  y2error("Floppy is not opend. Could not read map.
Please use OpenFloppy(...) to open floppy device.");
	  return $[];
      }

      NextStep(text);
      map return_map = WFM::Read(.local.ycp, mount_point + "/" + filename  );

      if ( return_map == nil  ||  size(return_map) == 0  )
      {  
	  //TODO check english
	  string message = sformat(_("\nThe file %1 cannot be read from %2.
"),filename, floppy_device  );

	  if ( use_ui )
	  {
	      Report::Error(message);
	  }
	  else {
	      y2error(message );
	  }
	  error = true;
	  return  $[];
      }
      return return_map;
  }

  /**
   * Save a ycp map to floppy. You must call OSRFloppy::Open( ) before.
   * You must call OSRFloppy::Open( ) before and afterwards OSRFloppy::Close().
   */
  global define boolean SaveMap(string filename, map file, string text )``{

      if( ! opened ) {
	  y2error("Floppy is not opend. Could not save map.
Please use OpenFloppy(...) to open floppy device.");
	  return false;
      }

      NextStep( text);
      boolean file_write = WFM::Write(.local.ycp, mount_point + "/"+ filename ,file );

      if ( ! file_write  )
      {  
	  //TODO check english
	  string message = sformat(_("The file %1 cannot be saved to %2."),filename, floppy_device  );
	  if ( use_ui )
	  {
	      Report::Error(message);
	  }
	  else {
	      y2error(message );
	  }
	  
	  return  false;
      }
      return true;
      
  }
    
}
